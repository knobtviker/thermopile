apply plugin: 'com.android.application'

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode buildConfig.version.code
        versionName buildConfig.version.name

        applicationId buildConfig.packageName + modules.drivers.suffix

        buildConfigField "String", 'GIT_COMMIT_SHA', "\"${gitSha()}\""
        buildConfigField 'long', 'GIT_COMMIT_TIMESTAMP', gitTimestamp().toString() + "L"

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def apkNameBuilder = new StringBuilder()

                apkNameBuilder.append(modules.drivers.archive)
                apkNameBuilder.append("_")
                apkNameBuilder.append(gitSha())
                apkNameBuilder.append("_")
                apkNameBuilder.append(output.baseName)
                apkNameBuilder.append(".apk")

                output.outputFileName = apkNameBuilder.toString().replace("-", "_")
            }
        }
    }

    buildTypes {
        debug {
            adbOptions {
                installOptions "-g"
            }
        }
        release {
            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a'
            universalApk false
        }
    }
}

dependencies {
    implementation project(path: deps.internal.shared)
    compileOnly deps.android.things
    implementation deps.android.annotations
    implementation deps.knobtviker.boards
    implementation deps.knobtviker.bme280
    implementation deps.knobtviker.bme680
    implementation deps.knobtviker.tsl2561
    implementation deps.knobtviker.ds3231
    implementation deps.knobtviker.lsm9ds1
    implementation deps.timber.android
}
