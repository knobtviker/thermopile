buildscript {

    ext.buildConfig = [
            'minSdk'     : 27,
            'compileSdk' : 28,
            'targetSdk'  : 28,

            'buildTools' : '28.0.1',

            'version'    : [
                    'major': 1,
                    'minor': 0,
                    'patch': 0,
                    'build': 0,
            ],

            'archive' : 'Thermopile',
            'packageName': 'com.knobtviker.thermopile',
    ]

    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    ext.buildConfig.version['fullName'] = "${buildConfig.version.name}.${buildConfig.version.build}"
    ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

    ext.modules = [
            'app'    : [
                    'suffix': '',
                    'archive'  : "${buildConfig.archive}_${buildConfig.version.fullName}",
            ],
            'drivers': [
                    'suffix': '.drivers',
                    'archive'  : "${buildConfig.archive}_Drivers_${buildConfig.version.fullName}",
            ],
            'fram'   : [
                    'suffix': '.fram',
                    'archive'  : "${buildConfig.archive}_Fram_${buildConfig.version.fullName}",
            ],
    ]

    ext.versions = [
            'things'       : '1.0',
            'annotations'  : '28.0.0-alpha3',
            'appcompat'    : '28.0.0-alpha3',
            'design'       : '28.0.0-alpha1',
            'recyclerview' : '28.0.0-alpha3',
            'constraint'   : '1.1.2',
            'navigation'   : '1.0.0-alpha03',
            'rxjava'       : '2.1.16',
            'rxandroid'    : '2.0.2',
            'dagger'       : '2.16',
            'joda'         : '2.9.9',
            'objectbox'    : '2.0.0-beta',
            'autovalue'    : '1.6',
            'butterknife'  : '8.8.1',
            'timber'       : '4.7.1',
            'firebase_core': '16.0.1',
            'crashlytics'  : '2.9.4@aar',
            'boards'       : '1.0.7',
            'bme280'       : '1.1.5',
            'bme680'       : '1.1.4',
            'tsl2561'      : '1.1.2',
            'ds3231'       : '1.0.5',
            'lsm9ds1'      : '1.0.4',
            'fram'         : '1.0.0',
    ]

    ext.deps = [
            'android'    : [
                    'things'      : "com.google.android.things:androidthings:${versions.things}",
                    'annotations' : "com.android.support:support-annotations:${versions.annotations}",
                    'appcompat'   : "com.android.support:appcompat-v7:${versions.appcompat}",
                    'design'      : "com.android.support:design-widget:${versions.design}",
                    'recyclerview': "com.android.support:recyclerview-v7:${versions.recyclerview}",
                    'constraint'  : "com.android.support.constraint:constraint-layout:${versions.constraint}",
                    'navigation'  : [
                            'fragment': "android.arch.navigation:navigation-fragment:${versions.navigation}",
                            'ui'      : "android.arch.navigation:navigation-ui:${versions.navigation}",
                    ],
            ],
            'rx'         : [
                    'java'   : "io.reactivex.rxjava2:rxjava:${versions.rxjava}",
                    'android': "io.reactivex.rxjava2:rxandroid:${versions.rxandroid}",
            ],
            'dagger'     : [
                    'runtime' : "com.google.dagger:dagger:${versions.dagger}",
                    'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
            ],
            'joda'       : [
                    'android': "net.danlew:android.joda:${versions.joda}",
            ],
            'objectbox'  : [
                    'browser': "io.objectbox:objectbox-android-objectbrowser:${versions.objectbox}",
                    'android': "io.objectbox:objectbox-android:${versions.objectbox}",
            ],
            'autovalue'  : [
                    'runtime' : "com.google.auto.value:auto-value-annotations:${versions.autovalue}",
                    'compiler': "com.google.auto.value:auto-value:${versions.autovalue}",
            ],
            'butterknife': [
                    'runtime' : "com.jakewharton:butterknife:${versions.butterknife}",
                    'compiler': "com.jakewharton:butterknife-compiler:${versions.butterknife}",
            ],
            'timber'     : [
                    'android': "com.jakewharton.timber:timber:${versions.timber}",
            ],
            'firebase'   : [
                    'core': "com.google.firebase:firebase-core:${versions.firebase_core}",
            ],
            'crashlytics': [
                    'android': "com.crashlytics.sdk.android:crashlytics:${versions.crashlytics}",
            ],
            'knobtviker' : [
                    'boards' : "com.knobtviker.android.things.contrib.community:boards:${versions.boards}",
                    'bme280' : "com.knobtviker.android.things.contrib.community.driver:bme280:${versions.bme280}",
                    'bme680' : "com.knobtviker.android.things.contrib.community.driver:bme680:${versions.bme680}",
                    'tsl2561': "com.knobtviker.android.things.contrib.community.driver:tsl2561:${versions.tsl2561}",
                    'ds3231' : "com.knobtviker.android.things.contrib.community.driver:ds3231:${versions.ds3231}",
                    'lsm9ds1': "com.knobtviker.android.things.contrib.community.driver:lsm9ds1:${versions.lsm9ds1}",
                    'fram'   : "com.knobtviker.android.things.contrib.community.driver:fram:${versions.fram}",
            ],
    ]

    repositories {
        google()
        maven { url "https://jitpack.io" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-alpha03'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'io.objectbox:objectbox-gradle-plugin:2.0.0-beta'
        classpath 'com.google.gms:google-services:4.0.1'
        classpath 'io.fabric.tools:gradle:1.25.4'
        classpath 'android.arch.navigation:navigation-safe-args-gradle-plugin:1.0.0-alpha03'
    }
}

allprojects {
    repositories {
        google()
        maven { url "https://dl.bintray.com/knobtviker/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://jitpack.io" }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def gitSha() {
    def f = new File(buildDir, "commit-sha.txt")
    if (!f.exists()) {
        def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
        if (p.waitFor() != 0) {
            throw new RuntimeException(p.errorStream.text)
        }
        f.parentFile.mkdirs()
        f.text = p.text.trim()
    }
    return f.text.trim()
}

def gitTimestamp() {
    def f = new File(buildDir, "commit-timestamp.txt")
    if (!f.exists()) {
        def p = 'git log -n 1 --format=%at'.execute([], rootDir)
        if (p.waitFor() != 0) {
            throw new RuntimeException(p.errorStream.text)
        }
        f.parentFile.mkdirs()
        f.text = p.text.trim()
    }
    return f.text.trim() + '000'
}
